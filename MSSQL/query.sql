------------------------SITE CHỦ---------------------------
GRANT EXECUTE ON OBJECT::[dbo].[SP_LayThongTinDangNhap_SV] TO SV; -- CAN XEM CAI NAY==============================================================
CREATE VIEW [dbo].[V_DS_PHANMANH]
AS
SELECT  TENCN=PUBS.description, TENSERVER= subscriber_server
   FROM dbo.sysmergepublications PUBS,  dbo.sysmergesubscriptions SUBS
   WHERE PUBS.pubid= SUBS.PUBID  AND PUBS.publisher <> SUBS.subscriber_server
GO
SELECT * FROM V_DS_PHANMANH

------------------------SITE 1,2,3 -------------------------
CREATE PROC [dbo].[SP_LayThongTinDangNhap]
@TENLOGIN NVARCHAR (50)
AS
BEGIN
DECLARE @TENUSER NVARCHAR(50), @UID INT, @HOTEN NVARCHAR(60) 
SELECT @UID= UID, @TENUSER=name FROM sys.sysusers  WHERE sid = SUSER_SID(@TENLOGIN)
	SELECT @HOTEN=HO+ ' '+ TEN FROM GIANGVIEN WHERE MAGV = @TENUSER
	IF @HOTEN IS NOT NULL
		BEGIN--(SELECT HO+ ' '+ TEN FROM GIANGVIEN WHERE MAGV = @TENUSER )
			SELECT USERNAME = @TENUSER, 		
				HOTEN = @HOTEN,
				TENNHOM= NAME
				FROM sys.sysusers 
				WHERE UID = (SELECT GROUPUID FROM SYS.SYSMEMBERS 
							   WHERE MEMBERUID= @UID)
		END
	ELSE
		RAISERROR('Login này không có quyền truy cập!',16,1);

END
GO
--Test
EXEC SP_LayThongTinDangNhap 'SV1'
GO
------------------------SITE 1,2 -------------------------
ALTER PROC [dbo].[SP_LayThongTinDangNhap_SV]
@TENLOGIN NVARCHAR(50),
@MASV NCHAR(10),
@PASSWORD NVARCHAR(40)
AS 
BEGIN
	DECLARE @HOTEN NVARCHAR(60) ,@MALOP NCHAR(10)
	SELECT @HOTEN = HO+ ' '+ TEN,@MALOP=MALOP FROM SINHVIEN WHERE MASV=@MASV AND PASSWORD=@PASSWORD
	IF @HOTEN IS NOT NULL
		BEGIN
			DECLARE @TENUSER NVARCHAR(50), @UID INT
			SELECT @UID= uid FROM sys.sysusers WHERE sid= SUSER_SID(@TENLOGIN)	-- @TENUSER= name
			SELECT MASV= @MASV,
					HOTEN =@HOTEN,
					MALOP= @MALOP,
					TENNHOM= NAME FROM sys.sysusers  WHERE UID = (SELECT GROUPUID FROM SYS.SYSMEMBERS WHERE MEMBERUID= @UID)
		END
	ELSE
		RAISERROR('Mã sinh viên không tồn tại!',16,1);
END
GO

ALTER PROC [dbo].[SP_LayThongTinDangNhap_SV]
@TENLOGIN NVARCHAR(50),
@MASV NCHAR(10),
@PASSWORD NVARCHAR(40)
AS 
BEGIN
	DECLARE @HOTEN NVARCHAR(60) 
	SELECT @HOTEN = HO+ ' '+ TEN FROM SINHVIEN WHERE MASV=@MASV AND PASSWORD=@PASSWORD
	IF @HOTEN IS NOT NULL
		BEGIN
			DECLARE @TENUSER NVARCHAR(50), @UID INT
			SELECT @UID= uid FROM sys.sysusers WHERE sid= SUSER_SID(@TENLOGIN)	-- @TENUSER= name
			SELECT MASV= @MASV,
					HOTEN =@HOTEN,
					TENNHOM= NAME FROM sys.sysusers  WHERE UID = (SELECT GROUPUID FROM SYS.SYSMEMBERS WHERE MEMBERUID= @UID)
		END
	ELSE
		RAISERROR('Mã sinh viên không tồn tại!',16,1);
END
GO
--Test--
EXEC SP_LayThongTinDangNhap_SV 'SV1','N15DCCN001','123456' 
GO
-----------------------SITE 1 -----------------------

-----------------------SITE 2 -----------------------
-----------------------SITE 1,2,3 -----------------------
CREATE PROC [dbo].[SP_TaoLogin]
  @LGNAME VARCHAR(50),
  @PASS VARCHAR(50),
  @USERNAME VARCHAR(50),
  @ROLE VARCHAR(50)
AS
  DECLARE @RET INT
  EXEC @RET= SP_ADDLOGIN @LGNAME, @PASS,'QLDSV_TC'
  IF (@RET = 1)  -- LOGIN NAME BI TRUNG
	BEGIN
		RETURN 1
	END
	
  EXEC @RET= SP_GRANTDBACCESS @LGNAME, @USERNAME
  IF (@RET = 1)  -- USER  NAME BI TRUNG
	BEGIN
       EXEC SP_DROPLOGIN @LGNAME
       RETURN 2
	END
  EXEC sp_addrolemember @ROLE, @USERNAME
  IF @ROLE='PKT' OR @ROLE= 'PGV' OR @ROLE='KHOA'
    BEGIN 
		EXEC sp_addsrvrolemember @LGNAME, 'SecurityAdmin'
		EXEC sp_addsrvrolemember @LGNAME, 'ProcessAdmin'
	END
RETURN 0  -- THANH CONG
GO 
-----------------------SITE 1,2,3 -----------------------
CREATE PROC [dbo].[SP_XoaLogin]
  @LGNAME VARCHAR(50),
  @USRNAME VARCHAR(50)
AS
  EXEC SP_DROPUSER @USRNAME
  EXEC SP_DROPLOGIN @LGNAME
GO



-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_LayNienKhoa] 
AS
	SELECT NIENKHOA FROM LOPTINCHI GROUP BY NIENKHOA
GO
--Test--
EXEC SP_LayNienKhoa 
GO
-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_LayHocKy_NK] 
@NienKhoa nchar(9)
AS
	SELECT HOCKY FROM LOPTINCHI WHERE NIENKHOA=@NienKhoa  GROUP BY HOCKY
GO
--Test--
EXEC SP_LayHocKy_NK '2021-2022'
GO
-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_LayMonHoc_HK] 
@NienKhoa nchar(9), 
@HocKy int
AS
	SELECT MONHOC.MAMH,MONHOC.TENMH 
	FROM (SELECT MAMH FROM LOPTINCHI AS tb_tmp WHERE NIENKHOA=@NienKhoa AND HOCKY = @HocKy) tb_tmp
	JOIN MONHOC
	ON tb_tmp.MAMH = MONHOC.MAMH
	GROUP BY MONHOC.MAMH,MONHOC.TENMH
GO
--Test--
EXEC SP_LayMonHoc_HK '2021-2022', 1
GO
-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_LayNhom_MH]
@NienKhoa nchar(9), 
@HocKy int,
@MonHoc nchar(10)
AS 
	SELECT NHOM FROM LOPTINCHI WHERE NIENKHOA=@NienKhoa AND HOCKY = @HocKy AND MAMH = @MonHoc
GO
--Test--
EXEC SP_LayNhom_MH '2021-2022', 1, 'CTDL'
GO
-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_LayLTC_N]
@NienKhoa nchar(9), 
@HocKy int,
@MonHoc nchar(10),
@Nhom int
AS
	SELECT MALTC FROM LOPTINCHI 
	WHERE NIENKHOA=@NienKhoa AND HOCKY = @HocKy AND MAMH = @MonHoc AND NHOM =@Nhom AND HUYLOP=0
GO
--Test--
EXEC SP_LayLTC_N '2021-2022', 1, 'CTDL',1
GO
-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_LayDSSV_LTC]
@NienKhoa nchar(9), 
@HocKy int,
@MonHoc nchar(10),
@Nhom int
AS
	declare @LOPTINCHI int
	SELECT  @LOPTINCHI= MALTC FROM LOPTINCHI 
	WHERE NIENKHOA=@NienKhoa AND HOCKY = @HocKy AND MAMH = @MonHoc AND NHOM =@Nhom AND HUYLOP=0

	SELECT DK.MALTC, DK.MASV, SINHVIEN.HO + ' ' +SINHVIEN.TEN AS HOTEN, DK.DIEM_CC, DK.DIEM_GK, DK.DIEM_CK,
	DK.DIEM_CC*0.1 + DK.DIEM_GK*0.3 + DK.DIEM_CK*0.6 AS DIEM_TK
	FROM (SELECT * FROM DANGKY WHERE MALTC = @LOPTINCHI AND (HUYDANGKY =0 OR HUYDANGKY IS NULL)) AS DK
	JOIN SINHVIEN
	ON DK.MASV =SINHVIEN.MASV
GO
--Test--
EXEC SP_LayDSSV_LTC '2021-2022', 1, 'CTDL',1
GO
-----------------------SITE 1,2 -----------------------
CREATE TYPE [dbo].[TYPE_DANGKY] AS TABLE(
	[MALTC] [int] NULL,
	[MASV] [nchar](10) NULL,
	[DIEM_CC] [int] NULL,
	[DIEM_GK] [float] NULL,
	[DIEM_CK] [float] NULL
)
GO
-----------------------SITE 1,2 -----------------------
CREATE PROC [dbo].[SP_CapNhatDiem]
@DIEMTHI TYPE_DANGKY READONLY 
AS 
BEGIN 
	MERGE INTO DANGKY AS TARGET
	USING (SELECT MALTC, MASV, DIEM_CC, DIEM_GK, DIEM_CK FROM @DIEMTHI) AS SOURCE
	ON TARGET.MALTC = SOURCE.MALTC AND TARGET.MASV = SOURCE.MASV 
	WHEN MATCHED THEN
		UPDATE SET TARGET.DIEM_CC = SOURCE.DIEM_CC,
				   TARGET.DIEM_GK = SOURCE.DIEM_GK,
				   TARGET.DIEM_CK = SOURCE.DIEM_CK
	WHEN NOT MATCHED THEN 
		INSERT ( MALTC, MASV, DIEM_CC, DIEM_GK, DIEM_CK)
		 VALUES (SOURCE.MALTC, SOURCE.MASV, SOURCE.DIEM_CC, SOURCE.DIEM_GK, SOURCE.DIEM_CK);
END 
GO
--Test--
DECLARE @DIEMTHI TYPE_DANGKY;
INSERT INTO @DIEMTHI (MALTC, MASV, DIEM_CC, DIEM_GK, DIEM_CK)
VALUES
    (1, 'N15DCCN006', 9, 8.0, 7.0);
EXEC [dbo].[SP_CapNhatDiem] @DIEMTHI;
GO
-----------------------SITE 3 -----------------------
CREATE PROC [dbo].[SP_LayHocPhi_SV]
@MASV NCHAR(10)
AS
IF exists (SELECT 1 FROM SINHVIEN a where a.MASV=@MASV)
	BEGIN 
		SELECT HPHI.NIENKHOA,HPHI.HOCKY,HPHI.HOCPHI, HPDADONG=ISNULL((select SUM(CT_HPHI.SOTIENDONG)),0),
		HPCONLAI=(HOCPHI-ISNULL((select SUM(CT_HPHI.SOTIENDONG)),0))  
		FROM (SELECT * FROM HOCPHI WHERE MASV=@MASV) HPHI 
		left join (SELECT * FROM CT_DONGHOCPHI WHERE MASV=@MASV) CT_HPHI
		on HPHI.NIENKHOA=CT_HPHI.NIENKHOA  AND HPHI.HOCKY=CT_HPHI.HOCKY
		GROUP BY HPHI.NIENKHOA,HPHI.HOCKY,HPHI.HOCPHI
	END
ELSE
  BEGIN 
    RAISERROR('Mã sinh viên không tồn tại!',16,1);
  END
--Test--
EXEC SP_LayHocPhi_SV 'N15DCCN001'
GO
-----------------------SITE 3 -----------------------
CREATE PROC [dbo].[SP_LayCTDongHocPhi_SV]
@MASV NCHAR(10),
@NIENKHOA NCHAR(9),
@HOCKY INT
AS
	BEGIN 
		SELECT NGAYDONG,SOTIENDONG FROM CT_DONGHOCPHI
		WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND @HOCKY=HOCKY
	END
GO
--Test--
EXEC SP_LayCTDongHocPhi_SV 'N15DCCN001','2021-2022',1
GO
-----------------------SITE 3 -----------------------
ALTER PROCEDURE [dbo].[SP_KTraDaDangKySV] 
@MASV NCHAR(10),
@NIENKHOA NCHAR(9),
@HOCKY INT
AS
	DECLARE @SOLTC_DK int
	SELECT @SOLTC_DK= COUNT(*) 
	FROM (SELECT MALTC,MASV FROM LINK1.QLDSV_TC.dbo.DANGKY WHERE MASV=@MASV) DK
	JOIN (SELECT MALTC,NIENKHOA,HOCKY FROM LINK1.QLDSV_TC.dbo.LOPTINCHI  WHERE NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY) LTC
	ON DK.MALTC=LTC.MALTC
	IF @SOLTC_DK>0
	BEGIN 
		RETURN 0;
	END
	SELECT @SOLTC_DK= COUNT(*) 
	FROM (SELECT MALTC,MASV FROM LINK2.QLDSV_TC.dbo.DANGKY WHERE MASV=@MASV) DK
	JOIN (SELECT MALTC,NIENKHOA,HOCKY FROM LINK2.QLDSV_TC.dbo.LOPTINCHI  WHERE NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY) LTC
	ON DK.MALTC=LTC.MALTC
	IF @SOLTC_DK>0
	BEGIN
		RETURN 0;
	END
	RETURN 1;
GO
EXEC SP_KTraDaDangKySV 'N14DCCN001','2021-2022',1
GO
-----------------------SITE 3 -----------------------
/*	DECLARE @SOLTC_DK int
	SELECT @SOLTC_DK= COUNT(*) 
	FROM (SELECT MALTC,MASV FROM DANGKY WHERE MASV=@MASV) DK
	JOIN (SELECT MALTC,NIENKHOA,HOCKY FROM LOPTINCHI  WHERE NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY) LTC
	ON DK.MALTC=LTC.MALTC
	IF @SOLTC_DK>0
	BEGIN	*/
CREATE PROC [dbo].[SP_ThemHocPhi]
@MASV NCHAR(10),@NIENKHOA NCHAR(9), @HOCKY INT,@HOCPHI INT
AS
	DECLARE @SV_DADK int
	EXEC @SV_DADK= [dbo].[SP_KTraDaDangKySV] @MASV,@NIENKHOA,@HOCKY
	IF @SV_DADK=0
	BEGIN 
		IF NOT EXISTS (SELECT * FROM HOCPHI WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY)
			BEGIN
				INSERT INTO HOCPHI(MASV,NIENKHOA,HOCKY,HOCPHI)
				VALUES(@MASV,@NIENKHOA,@HOCKY,@HOCPHI)
			END
		ELSE
		  RAISERROR('Học phí học kỳ này của sinh viên đã tồn tại!',16,1)
	END
	ELSE
		RAISERROR('Không thể thêm học phí! Sinh viên chưa đăng ký lớp tín chỉ trong niên khóa này!',16,1)
 GO

 --TEST--
 EXEC SP_ThemHocPhi 'N15DCCN003','2021-2022',1,1000000
 EXEC SP_ThemHocPhi 'N15DCCN004','2021-2022',1,1000000
GO
-----------------------SITE 3 -----------------------
CREATE PROC [dbo].[SP_CapNhatHocPhi]
@MASV NCHAR(10),@NIENKHOA NCHAR(9), @HOCKY INT,@HOCPHI INT
AS
	IF EXISTS (SELECT * FROM CT_DONGHOCPHI WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY)
		BEGIN
			RAISERROR('Không thể cập nhật học phí! Sinh viên đã đóng học phí của học kỳ này!',16,1)
		END
	ELSE
			UPDATE HOCPHI
			SET HOCPHI=@HOCPHI
			WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY
GO
--TEST--
EXEC SP_CapNhatHocPhi 'N15DCCN001', '2021-2022',1, 2000000
GO
-----------------------SITE 3 -----------------------
CREATE PROC [dbo].[SP_ThemCTDongHocPhi]
@MASV NCHAR(10),@NIENKHOA NCHAR(9), @HOCKY INT,
@NGAYDONG DATE,@SOTIENDONG INT
AS
	IF EXISTS (SELECT * FROM CT_DONGHOCPHI WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY AND @NGAYDONG =NGAYDONG)
		RAISERROR('Không thể thêm chi tiết đóng học phí!\nThông tin đóng học phí này của sinh viên này đã tồn tại!',16,1)
	ELSE 
	    BEGIN
			INSERT INTO CT_DONGHOCPHI(MASV,NIENKHOA,HOCKY,NGAYDONG,SOTIENDONG)
			VALUES(@MASV,@NIENKHOA,@HOCKY,@NGAYDONG,@SOTIENDONG)
		END
GO
--Test--
Exec SP_ThemCTDongHocPhi 'N15DCCN001','2021-2022',1,'2024-06-23',100000
GO
-----------------------SITE 3 -----------------------
CREATE PROC [dbo].[SP_XoaHocPhi]
@MASV NCHAR(10),@NIENKHOA NCHAR(9), @HOCKY INT
AS
	IF EXISTS (SELECT 1 FROM CT_DONGHOCPHI WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY)
		BEGIN
			RAISERROR('Không thể xóa học phí!\nSinh viên đã đóng học phí học kỳ này! ',16,1)
		END
	ELSE
	  BEGIN
			DELETE  FROM HOCPHI
			WHERE MASV=@MASV AND NIENKHOA=@NIENKHOA AND HOCKY=@HOCKY
	  END
GO

EXEC SP_XoaHocPhi 'N15DCCN004','2021-2022',1
GO
-----------------------SITE 3 -----------------------
CREATE PROCEDURE SP_BaoCaoHocPhi
@MALOP nchar(10), 
@NIENKHOA nchar(9), 
@HOCKY int 
AS
	SELECT DSSV.MASV, DSSV.HOTEN, HP.HOCPHI, ISNULL(HPCT.HPDADONG,0) AS HPDADONG
	FROM (SELECT MASV,HO+' '+TEN AS HOTEN FROM SINHVIEN WHERE MALOP = @MALOP) DSSV
	JOIN (SELECT MASV,HOCPHI FROM HOCPHI WHERE NIENKHOA = @NIENKHOA AND HOCKY =@HOCKY ) HP
	ON HP.MASV = DSSV.MASV 
	LEFT JOIN   (SELECT MASV,SUM(SOTIENDONG) AS HPDADONG FROM CT_DONGHOCPHI WHERE NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY  GROUP BY MASV) HPCT
	ON DSSV.MASV=HPCT.MASV
GO
--Test--
EXEC SP_BaoCaoHocPhi 'D15CQCP01','2021-2022',1
GO
-----------------------SITE 3 -----------------------
GRANT EXECUTE ON OBJECT::dbo.SP_LayThongTinDangNhap TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_CapNhatHocPhi TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_LayCTDongHocPhi_SV TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_LayHocPhi_SV TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_TaoLogin TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_XoaHocPhi TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_XoaLogin TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_ThemHocPhi TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_ThemCTDongHocPhi TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_KTraDaDangKySV TO PKT;
GRANT EXECUTE ON OBJECT::dbo.SP_BaoCaoHocPhi TO PKT;

-----------------------SITE 1,2 -----------------------
GRANT EXECUTE ON OBJECT::dbo.SP_LayThongTinDangNhap_SV TO SV;
GRANT EXECUTE ON OBJECT::dbo.sp_LayDSLopTinChiDeDangKy TO SV;
GRANT EXECUTE ON OBJECT::dbo.sp_LayDSLopTinChiDaDangKy TO SV;
GRANT EXECUTE ON OBJECT::dbo.sp_PhieuDiem TO SV;
GRANT EXECUTE ON OBJECT::dbo.sp_DangKyLopTinChi TO SV;
GRANT EXECUTE ON OBJECT::dbo.SP_LayNienKhoa TO SV;
GRANT EXECUTE ON OBJECT::dbo.SP_LayHocKy_NK TO SV;


--ko cap quyen cho cai nay
GRANT EXECUTE ON OBJECT::dbo.sp_InBangDiemTongKet TO SV;
